import:std.stacksy

@board:900

$width:30
$height:30

$iterations:100

#setstar
	copy '*' set:1 
#

#setspace
	copy ' ' set:1 
#

#entry
	@board copy $width $height * + swap
	while copy:1 copy:1 > do
		setspace
		1 +
	elihw pop pop

	@board 
	setstar
	
	$width 1 + +
	setstar
	
	1 +
	setstar
	
	$width 2 - +
	setstar
	
	1 +
	setstar
	
	pop
	$iterations
	while copy 0 > do
		clear
		print_gameoflife
		run_gameoflife
		"Press enter to continue" print
		readbyte pop
		1 -
	elihw
#

@x:1
@y:1
@sum:1

#run_gameoflife
	@x 0 set:1
	while @x get:1 $width < do
		@y 0 set:1
		while @y get:1 $height < do
			@sum 0 set:1
			@x get:1 1 - @y get:1 1 - check_tile
			@x get:1 1 - @y get:1 check_tile
			@x get:1 1 - @y get:1 1 + check_tile
			@x get:1 1 + @y get:1 1 - check_tile
			@x get:1 1 + @y get:1 check_tile
			@x get:1 1 + @y get:1 1 + check_tile
			@x get:1 @y get:1 1 - check_tile
			@x get:1 @y get:1 1 + check_tile
			
			@x get:1 @y get:1 get_tile_content '*' = if
				@sum get:1 2 != @sum get:1 3 != & if
					'_' @x get:1 @y get:1 set_tile_content
				fi
			else
				@sum get:1 3 = if
					'#' @x get:1 @y get:1 set_tile_content
				fi
			fi
			@y copy get:1 1 + set:1
		elihw
		@x copy get:1 1 + set:1
	elihw
	
	
	; Change # to * and _ to ' '
	@board $width $height * + @board
	while copy:1 copy:1 > do
		copy get:1 '#' = if
			setstar
		else
			copy get:1 '_' = if
				setspace
			fi
		fi
		1 +
	elihw
	pop pop
#

; x y
#get_tile_content
	swap $width * +
	@board + get:1 
#

; c x y
#set_tile_content
	swap $width * +
	@board + swap set:1 
#

; x y
#check_tile
	copy $width < if
		copy 0 >= if
			swap copy $height < if
				copy 0 >= if
					swap copy:1 copy:1 get_tile_content
					copy '*' = swap '_' = or if
						@sum copy get:1 1 + set:1
					fi
				fi
			fi
		fi
	fi
	pop pop
#

#print_gameoflife
	@x 0 set:1
	while @x get:1 $width < do
		@x get:1 $width * @board + $height printn
		"\n" print
		
		@x copy get:1 1 + set:1
	elihw
#